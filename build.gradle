plugins {
    id 'java-library'
    id 'eclipse'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.gradle.userdev' version '7.0.153'
}

version = "0.12.2"
group= "dev.gigaherz.jsonthings"

Provider<String> minecraftVersion(Transformer<String, String> mapper) {
    return project.userDevRuntime.runtimes.map(r -> r.values().first().specification.minecraftVersion).map(mapper)
}

base {
    archivesName = minecraftVersion { "JsonThings-${it}" }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)


tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8' // Use the UTF-8 charset for Java compilation
}

repositories {
    maven {
        name = "Dogforce Games maven"
        url 'https://www.dogforce-games.com/maven/'
    }
    maven {
        url 'https://repo.spongepowered.org/repository/maven-public/'
        content {
            includeGroup "org.spongepowered"
        }
    }
    maven {
        url "https://cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    mavenLocal()
}

runs {
    // applies to all the run configs below
    configureEach {
        systemProperty 'forge.logging.markers', '' // 'REGISTRIES'
        systemProperty 'forge.logging.console.level', 'debug'

        //ideaModule "${project.name}.main"

        workingDirectory project.file('run')

        modSource project.sourceSets.main
    }

    client {
    }

    server {
    }
}

subsystems {
    parchment {
        minecraftVersion="1.21"
        mappingsVersion="2024.06.23"
    }
}

minecraft.accessTransformers.file rootProject.file('src/main/resources/META-INF/accesstransformer.cfg')

jarJar.enable()

dependencies {
    implementation 'net.neoforged:neoforge:21.0.86-beta'

    //library 'com.google.guava:guava:21.0'
    //runtimeOnly fg.deobf("useless:Botania:1.18.2.433")
    //runtimeOnly fg.deobf("useless:curios-forge-1.18.2:5.0.7.0")
    //runtimeOnly fg.deobf("useless:Patchouli:1.18.2.69")

    //compileOnly 'curse.maven:rhino-416294:4805923'
    compileOnly 'dev.gigaherz.rhinolib:rhino-lib:1.0.1'
    runtimeOnly 'dev.gigaherz.rhinolib:rhino-lib:1.0.1'
    jarJar(group:'dev.gigaherz.rhinolib', name:'rhino-lib', version:'[1.0-SNAPSHOT,)')  {

    }

    /*library 'org.graalvm.regex:regex:22.0.0.2'
    library 'org.graalvm.sdk:graal-sdk:22.0.0.2'
    library 'org.graalvm.truffle:truffle-api:22.0.0.2'
    library 'org.graalvm.js:js:22.0.0.2'
    library 'org.graalvm.js:js-scriptengine:22.0.0.2'*/
}

java {
    withSourcesJar()
}

tasks.named("jar") {
    archiveClassifier = "slim"

    from('/') {
        include 'LICENSE.txt'
    }

    manifest {
        attributes([
                "Specification-Title": "jsonthings",
                "Specification-Vendor": "gigaherz",
                "Specification-Version": "1", // We are version 1 of ourselves
                "Implementation-Title": project.name,
                "Implementation-Version": project.version,
                "Implementation-Vendor" :"gigaherz",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.named("jarJar") {
    archiveClassifier = ""
}

tasks.withType(GenerateModuleMetadata) {
    enabled = false
}

project.afterEvaluate {
    publish.dependsOn('build')
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId project.archivesBaseName
                from components.java

                //jarJar.component(it)
                //artifact sourceJar

                pom.withXml {
                    asNode().dependencies.dependency.each { dep ->
                        if (dep.version.last().value().last().contains("mapped")) {
                            assert dep.parent().remove(dep)
                        }
                    }
                }
            }
        }

        repositories {
            if (findProperty("RELEASE") && System.env.giga_maven_host != null) {
                System.out.println("Remote publish enabled on " + System.env.giga_maven_host)
                maven {
                    url System.env.giga_maven_host
                    credentials {
                        username System.env.giga_maven_user
                        password System.env.giga_maven_password
                    }
                }
            }
            else {
                System.out.println("Remote publish disabled.")
                maven {
                    url "$projectDir/../CommonMaven"
                }
            }
        }
    }
}

// IDEA no longer automatically downloads sources/javadoc jars for dependencies, so we need to explicitly enable the behavior.
idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}